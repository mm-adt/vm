:imagesdir: ./images/obj

== The Obj Graph

An mm-ADT program is a *type*. The `mmlang` parser converts a textual representation of a type into a type `obj`. A type is inductively defined and is encoded as a https://en.wikipedia.org/wiki/Path_(graph_theory)[path] within the larger *type graph*. The type's path is a graphical encoding specifying a data flow https://en.wikipedia.org/wiki/Pipeline_(computing)[pipeline] that when evaluated, constructs elements of the type (i.e. computed resultant values). These values also have a graphical encoding paths in the *value graph*. Together, the type graph and the value graph form the https://en.wikipedia.org/wiki/Quiver_(mathematics)[quiver] known as the *obj graph*.

Every aspect of an mm-ADT computation from composition, to compilation, and ultimately to evaluation is materialized in the `obj` graph. The following itemizations summarizes the various roles that the `obj` graph throughout a computation.

* *Composition*: The construction of a type via the https://en.wikipedia.org/wiki/Tacit_programming[point-free] style of `mmlang` is a the lexical correlate of walking the `obj` graph from a source vertex (domain ctype) across a series of instruction-labeled edges (`inst`) to ultimately arrive at a target vertex (range ctype). The path, a https://en.wikipedia.org/wiki/Free_object[free object], contains both the type's signature and definition.
* *Compilation*: A path in the type graph can be prefixed with another ctype (e.g. placing `int` before `_`). In doing so, the path's domain has been alterered and the path is recomputed to potentially yield a variant of the original path (e.g. a type inferenced path).
* *Rewrite*: Subpaths of a path in the type graph can be specified as being semantically equivalent to another path in the type graph via `poly` lifted rewriting `(y)\<=(x)`. Subsequent compilations and evaluations of the path may yield path variants.
* *Optimization*: Every instruction in `inst` has an associated cost dependent on the underlying storage and processor. Rewrites create a superposition of programs. Given that the `obj` graph https://en.wikipedia.org/wiki/Commutative_diagram[commutes], a https://en.wikipedia.org/wiki/Dijkstra%27s_algorithm[weighted shortest path] calculation from a domain vertex to a range vertex is an example of a simple technique for choosing an efficient execution plan.
* *Variables*: Variable bindings are encoded in instructions. When the current instruction being evaluated requires historic state information, the `obj`^op^ graph (with edges reversed) is searched in order to locate the vertex incident to a variable `inst`.
* *Evaluation*: Program evaluation binds the type graph to the value graph. When a type path is prefixed with a value `obj`, the instructions along the path operate on the value, where the path's target vertex is the result of the computation.

This section will discuss the particulars of the aforementioned uses of the `obj` graph.

=== State

Let \$(M,\cdot,e)\$ be a https://en.wikipedia.org/wiki/Monoid[monoid], where \$e \in M\$ is the https://en.wikipedia.org/wiki/Identity_element[identity element] and there exists an element \$e' \in M\$ that also acts as an identity such that for every \$ x \in M \$, \$x \cdot e = x\$ and \$x \cdot e' = x\$, then because \$e \cdot e' = e\$ and \$e \cdot e' = e'\$, it is the case that \$e = e \cdot e' = e'\$ and \$e = e'\$.
Thus, every monoid has a *single unique identity*.
However, in a https://en.wikipedia.org/wiki/Free_monoid[free monoid], where element composition history is preserved, it is possible to record \$e\$ and \$e'\$ as distinctly _labeled_ elements even though their role in the non-free monoid's binary composition are the same -- namely, that they both act as identities.

[cols="2,1,2" width=50,float=right]
|====
| idiom                                                                      | inst           | description

| https://en.wikipedia.org/wiki/Variable_%28computer_science%29[variables]   | `[to]`         | `obj` references
| https://en.wikipedia.org/wiki/Data_type[type definitions]                  | `[define]`     | `type` mappings
| https://en.wikipedia.org/wiki/Category_(mathematics)[models]               | `[model]`      | domain of discourse
| https://en.wikipedia.org/wiki/Reversible_computing[reversible computing]   | `[path]`       | computing history
|====

It is through *multiple distinct identities* in `inst` that mm-ADT supports the programming idioms in the associated table. The general approach is __state is stored along the path of the `obj`.__

[exec]
----
6 => int[plus,[mult,2]][path]
8 => int[plus,[mult,2]][path]
----

Every `obj` exists as a distinct vertex in the `obj` graph. If \$b \in \tt{obj}\$ has an incoming edge labeled \$i \in \tt{i\nst}\$, then when applied to the outgoing adjacent vertex \$a\$, \$b\$ is computed. Thus, the edge \$a \to_i b\$ records the instruction and incoming `obj` (\$a\$) that yielded the `obj` at the head of the edge (\$b\$). Since types are defined *inductively* and their respective values generated *deductively* via instruction evaluation along the type's *path*, the path contains all the information necessary to effect https://en.wikipedia.org/wiki/State_(computer_science)[state]-based computing. The path of an `obj` is accessed via the `[path]` instruction. The output of `[path]` is a `;-lst` -- i.e., an element of the `inst` https://en.wikipedia.org/wiki/Syntactic_monoid[syntactic monoid]. This path `lst` is also a https://en.wikipedia.org/wiki/Product_(category_theory)[product] and as such, can be https://en.wikipedia.org/wiki/Reflection_(computer_programming)[introspected] via it's projection morphisms (e.g., via `[get]`).

[exec]
----
8 => int[plus,1][mult,2][lt,63]                                                           //<1>
8 => int[plus,1][mult,2][lt,63][path]                                                     //<2>
8 => int[plus,1][mult,2][lt,63][path][get,5][get,0]                                       //<3>
----
<1> The evaluation of an `bool\<=int` type via 8.
<2> The `obj` graph path from 8 to `[lt,63]`.
<3> A projection of the instruction `[lt,63]` from the path and then the first argument of the `inst`.

mm-ADT's *multiple identity instructions* simply compute the identity function \$f(x) \mapsto x\$, but as edge labels in the `obj` graph, they store state information that can be later accessed via trace-based path analysis (i.e. via `[path]`).
In effect, the execution context is transformed from a memory-less https://en.wikipedia.org/wiki/Finite-state_machine[finite state automata] to a https://en.wikipedia.org/wiki/Processor_register[register]-based https://en.wikipedia.org/wiki/Turing_machine[Turing machine].

==== Variables

The `[to]` instruction's type definition is `a\<=a[to,_]`. The argument to `[to]` is a *named anonymous type*. For every incoming \$a \in \tt{obj}\$, there is an outgoing \$a\$ whose path has been extended with the `[to]` instruction. An example is provided below.

[.center]
[cols=^.^1,width=40,grid=none,frame=none]
|====
a|
[exec,eval=false,prompt=""]
----
_[plus,1][to,x][plus,2][mult,x]
----
{blank}
|====

Suppose `int` is applied to the above anonymous type. This triggers a cascade of events whereby `[plus,1]` maps `int` to `int[plus,1]`, then `[to,x]` maps `int[plus,1]` to `int[plus,1][to,x]`, and so forth. The resultant compiled `int`-type can then be evaluated by an `int` value such as 9. In the commuting diagram below, the top instruction sequence forms a value graph (*evaluation*), the middle sequence a type graph (*compilation*), and the bottom, an __ un__typed graph (*composition*). The union of these graphs via the https://en.wikipedia.org/wiki/Inclusion_map[inclusion morphism] (`[type]`) is the complete `obj` graph of the computation.

NOTE: In `mmlang`, the `[to]` instruction's sugar is `< >`. It is the only instruction whose sugar is printed as opposed to its `[ ]` form.

[.center]
[cols="1",width=50,frame=none,grid=none]
|====
a|
[exec]
----
_   => [plus,1]<x>[plus,2][mult,x]     ## _[plus,1]<x>[plus,2][mult,x]
int => _[plus,1]<x>[plus,2][mult,x]    ## int[plus,1]<x>[plus,2][mult,x]
9   => int[plus,1]<x>[plus,2][mult,x]  ## 120
----
{blank}
|====

image::linear-variable-example.svg[align=center,width=600]

image::linear-variable-example-2.svg[align=center,width=350,float=right]

The primary idea concerning variable state is that when `[mult,x]` is reached by the `int` value 12 via instruction application, the anonymous type `x`  must be *resolved* before `[mult]` can evaluate. To do so, the instruction `[to,x]` is searched for in the path history of 12. When that instruction is found, the range (or domain as it's an identity) replaces `x` and `[mult,10]` is evaluated and the edge \[12 \rightarrow_{\texttt{[mult,10]}} 120 \] extends the value graph. The intuition for this process is illustrated on the right.

[exec]
----
9 => int[plus,1]<x>[plus,2][mult,x][path]                                                 //<1>
int[plus,1]<x>[plus,2][mult,x][explain]                                                   //<2>
----
<1> The `[path]` instruction provides the path of the current `obj` as a `;-lst`.
<2> The `[explain]` instruction details the scope of state variables.

The variable's https://en.wikipedia.org/wiki/Scope_(computer_science)[scope] starts at `[to]` and ends when there is no longer a path to `[to]`. If an `inst` argument is a type (e.g. `[mult,[plus,x]]`), then the *child type* (`[plus,x]`) path extends the *parent type* (`[mult]`) path. As such, the child type has access to the variables declared in the parent composition up to the `inst` containing the child type (`[mult]`). Finally, if `[to,x]` is evaluated and later along that path `[to,x]` is evaluated again, all subsequent types will resolve `x` at the latter `[to,x]` instruction. That is, the graph search halts at the first encounter of `[to,x]` -- the https://en.wikipedia.org/wiki/Shortest_path_problem[shortest path] to a declaration.

[exec]
----
2 => int<x>[plus,<y>][plus,y]                                                             ## LanguageException //<1>
2 => int<x>[plus,[plus,x]<x>[plus,x]][plus,x]                                             ## 12                //<2>
2 => int<x>[plus,[plus,x]<x>[plus,x]][plus,x][path]                                       //<3>
int<x>[plus,int<y>[plus,int<z>[plus,x][plus,y][plus,z]][plus,y]][plus,x][explain]         //<4>
----
<1> The variable `y` is declared in a branch nested within the retrieving branch.
<2> The variable `x` is redefined in the nested branch and recovers its original value when the nested branch completes.
<3> The value path of the previous evaluation highlighting that the final `[plus,x]` resolved to `[plus,2]`.
<4> A multi-nested expression demonstrating the creation and destruction of variable scope.

==== Definitions

A *type definition* takes one of the two familiar forms
\[
b<=a
\]
or
\[
b:a
\]
where, for the first, \$b\$ is _generated_ by \$a\$ and for the second, \$b\$ is _structured_ as \$a\$ and, when considering no extending instructions to the \$b<=a\$ form, \$b<=a \cong b:a\$ such that \$a\$ is _named_ \$b\$. For most of the documentation, the examples have been presented solely from within the `mm` model-ADT where there are 6 types: `bool`, `int`, `real`, `str`, `lst`, and `rec` along with their respective instructions. It is possible to extend `mm` with new types that are ultimately _grounded_ (Cayley rooted) in the `mm` model-ADT types. This is the purpose of the `[define]` instruction which will now be explained by way of example.


The natural numbers (\$\mathbb{N}\$) are a https://en.wikipedia.org/wiki/Refinement_type[refinement] of the set of integers (\$\mathbb{Z}\$), where \$\mathbb{N} \subset \mathbb{Z}\$. In https://en.wikipedia.org/wiki/Set-builder_notation#Sets_defined_by_a_predicate[set builder notation], specifying the set of integers and a predicate to limit the set to only those integers greater than 0 is denoted
\[
\mathbb{N} = \{n \in \mathbb{Z} \;|\; n > 0\}.
\]
In mm-ADT, `int` is a `nat` (\$\mathbb{N}\$) if there is a *path* through the type graph from the `int` to `nat`. These paths are *type definitions*. In the example below, `[define]` creates a path from at `int` to `nat` via the instruction `[is>0]`.

[.center]
[cols=^1,width=25,frame=none,grid=none]
|===
a|
[exec,prompt="",eval=false]
----
[define,nat<=int[is>0]]
----
{blank}
|===

A `nat` is any `int` that arrives at `nat` via `nat\<=int[is>0]`. Given this definition (and this definition only), `nat` is a _refinement_ of `int` because only 50% of `ints` successfully reach `nat`. However, there may be other paths to `nat` from other types and as such, https://en.wikipedia.org/wiki/Refinement_type[type refinement] is a relative concept in mm-ADT. In isolation, `nat` is only a character label (called a *name*) attached to a vertex in the `obj` graph. There is no other structure to a isolated type. The nature of a type is completely determined by the paths incoming and outgoing from it. In this https://en.wikipedia.org/wiki/Graph_theory[graph-based interpretation] of types, a type can be the source or target of any number of paths and it is through navigating these paths that values at a type are morphed into values at other types, where mm-ADT instructions (`inst`) specify, step-by-step, the way in which the morphing process is to be carried out.

[exec]
----
36 => int[define,nat<=int[is>0]]                   ## 36                //<1>
36 => nat<=int[define,nat<=int[is>0]]              ## nat:36            //<2>
36 => nat<=int[define,nat<=int[is>0]][mult,-1]     ## LanguageException //<3>
----
<1> A `nat` is defined, but never applied. Thus, logically, this is equivalent to `36 \=> int`.
<2> A type can be used anytime after its definition in the path. Thus, `nat` is a viable range type.
<3> If the `obj` is not a `nat`, then the larger `nat\<=int` is invalid.

.mm-ADT Type Prefix
================================================================
[cols=1,width=50,float=right,grid=none,frame=none]
|====
a|
[exec,none=""]
----
:[model,mm][define,nat<=int[is>0]]
10 => nat                          ## nat:10
-1 => nat                          ## LanguageException
10 => nat[plus,5]                  ## nat:15
10 => nat[plus,5][plus,-15]        ## LanguageException
----
{blank}
|====

Prepending type definitions to every program reduces legibility and complicates program development. For this reason, mm-ADT provides a *type prefix*. All `mmlang` examples that start with `:` are defining _the_ type prefix that will be used for all subsequent programs. The type prefix is a generalization of a https://en.wikipedia.org/wiki/Comparison_of_programming_languages_(syntax)#Libraries[library statement] such as `import` or `module` found in other programming languages. The generalization is that a type prefix can be any type, not just those containing only `[define]`). The type prefix is prepended to the program type prior to compilation, where this operation is made sound by the free `inst` monoid.
================================================================

[cols="^.1,^.^2",width=15,grid=none,float=left]
|====
1.5+|
\[
\begin{split}
\hookrightarrow \\
\rightarrowtail \\
\twoheadrightarrow \\
\sim \\
{=}
\end{split}
\]
1.5+|
https://en.wikipedia.org/wiki/Embedding[inclusion]
https://en.wikipedia.org/wiki/Monomorphism[monomorphism]
https://en.wikipedia.org/wiki/Epimorphism[epimorphism]
https://en.wikipedia.org/wiki/Isomorphism[isomorphism]
https://en.wikipedia.org/wiki/Equality_(mathematics)[equality]
|====

The example below defines a `date` to be a `;-lst` with 2 or 3 `nats`. If the `;-lst` contains only 2 terms, then a default value of 2020 is provided. This highlights an important aspect of mm-ADT's type system. Variables, types, and rewrites are all _graph search processes_. A defined type (path) with a desired *range* is searched for in the `obj` graph and returned if and only if the morphing `obj` matches the defined type's *domain*. Type definitions are simply other types that specify the means by which one type is translated into another type. To the left, the meaning of the arrows' https://en.wikipedia.org/wiki/Commutative_diagram[graphical annotations] are provided.


image::date-example.svg[]

[exec]
----
:[model,mm][define,nat<=int[is>0],                              %%
                   date<=(nat[is=<12];nat[is=<31];nat),         %
                   date<=(nat[is=<12];nat[is=<31])[put,2,2020]]
(8;26;2020) => date ## date:(nat:8;nat:26;nat:2020)
(8;26)      => date ## date:(nat:8;nat:26;nat:2020)
----

Defining types with `[define]` is useful for _in situ_ definitions that are only require through the scope of the definition (typically within nested types). For reusing types across mm-ADT programs, mm-ADT offers _models_ and the `[model]` instruction.

=== Models

.Homotopy Type Theory
****
https://en.wikipedia.org/wiki/Homotopy_type_theory[Homotopy type theory] understands types as coexisting with other types in a https://en.wikipedia.org/wiki/Topology[topological] space. A spatial embedding implies that a type can be reached from another type by moving through space. The way in which the space is navigated is via paths. A path is a continuous deformations of one type into another type.
****

Types can be organized into *model-ADTs* (simply called *models*). The 4 _mono_ types (`bool`,`int`,`real`,`str`) and the 2 _poly_ types (`lst`, `rec`) are defined in the `mm` model-ADT (the _mm_ of _mm_-ADT). The instruction `[model,mm]` generates a `rec` from the `mmlang` file `mm.mm`. Using the same multiplicity of identities principle, the `rec` is accessible in the type's path definition via the `[model]` argument.

[.center]
[cols="6,^6",width=90,separator=¦]
|====
a¦
[source]
----
include::../../src/main/resources/model/mm.mm[lines="1..4"]
))
----
{blank}
¦
image:mm-model.svg[width=340,align=center]
|====

The `rec` encoding of a model-ADT has the model's canonical types (*ctypes*) as keys and `lsts` of derived types (*dtypes*) as values. The encoding is a serialization of a graph where the ctypes are vertices and the incoming paths to a ctype vertex are the edges. Unfortunately, the `mm` model is too basic to demonstrate this point clearly. What `mm` does capture is the set nature of the base types in that there are vertices and no edges (save `poly` which is the https://en.wikipedia.org/wiki/Coproduct[coproduct] of `lst` and `rec`).

In general, any model \$\mathbf{m}\$ is defined
\[
\begin{split}
\texttt{model}_\mathbf{m} &= \coprod_{i=0}^{|\mathbf{m}|} \texttt{ctype}_i + (\texttt{dtype}_i^0 + \texttt{dtype}_i^1 + \ldots + \texttt{dtype}_i^n) \\
&= \coprod_{i=0}^{|\mathbf{m}|} \texttt{ctype}_i + \coprod_{j=0}^{|\texttt{dtype}_i|} \texttt{dtype}_i^j.
\end{split}
\]
There are more ctypes than the 6 https://en.wikipedia.org/wiki/Primitive_data_type[base types] specified in `mm`. Typically, a ctype in one model is a dtype in another. If model \$\mathbf{m}\$ has \$\tt{ctypes}_\mathbf{m}\$ derived from types in model \$\mathbf{n}\$, then \$\tt{dtypes}_{\mathbf{n}} \subseteq \tt{ctypes}_{\mathbf{m}}\$. However, `mm` is unique in that the `mm` types are _universally grounded_ and
\[
\begin{split}
\textbf{mm} &= \coprod_{i=0}^6 \texttt{ctype}_i + \mathbf{0}_i \\
&= \coprod_{i=0}^6 \texttt{ctype}_i \\
&= (\texttt{bool} + \texttt{int} + \texttt{real} + \texttt{str} + \texttt{lst} + \texttt{rec})
\end{split}
\]

That is, `mm` is the sum of 6 ctypes -- the mm-ADT base types. Within `mm`, these ctypes are https://en.wikipedia.org/wiki/Identity_function[identity types]. For example, in the `mm` model `rec` at the beginning of this section, the field `bool \-> ( )` denotes \$\tt{bo\ol} \+ \mathbf{0}\$ or simply \$\tt{bo\ol}\$. The `bool` ctype is shorthand for `bool\<=bool`, which, when considering the quantifier ring, is shorthand for `bool{1}\<=bool{1}`. An instruction less type is a https://en.wikipedia.org/wiki/NOP_(code)[`noop`] and thus, `bool` captures the https://en.wikipedia.org/wiki/Reflexive_relation[reflexivity] of identity:
\[
\texttt{bool}\Leftarrow\texttt{bool} \;\equiv\; \texttt{bool} + \mathbf{0} \;\equiv\; \texttt{bool}.
\]

.Model-ADT Subgraphs of the `obj` Graph
================================================================
image::model-adt.svg[width=450,float=right]

The associated illustration presents 3 models, their respective ctypes, and various dtypes between them. Every directed labeled binary edge in the diagram is a type of the form:

[.center]
[cols=^1,frame=none,grid=none,width=20]
|===
a|
[source]
----
b<=a[inst{*}]
----
{blank}
|===

A type definition's instructions specify the specific, https://en.wikipedia.org/wiki/Discrete_system[discrete] computational steps (`inst`) necessary to transform `a` (domain) into `b` (range). A series of instructions are constructed with https://en.wikipedia.org/wiki/Inductive_type[type induction] (composition), destructed with https://en.wikipedia.org/wiki/Type_inference[type deduction] (compilation or evaluation), and are captured as paths in the type subgraph of the `obj` graph. Thse paths are equivalent to the morphisms of the `obj` https://en.wikipedia.org/wiki/Diagram_(category_theory)[category diagram] and the edges in the `obj` https://en.wikipedia.org/wiki/Cayley_graph[Cayley graph]. The illustration highlights three sorts of types:

* `xtype\<=int[f]` (**intra-model**): In `xmodel`, `xtype` is grounded at `int` in `mm`.
* `ytype\<=xtype[g]` (**inter-model**): In `xmodel`, `ytype` can be reached via `xtype`.
* `atype\<=rec[h][i]` (**trans-model**): In `amodel`, `atype` is grounded at `rec` in `mm` via `ytype` in `xmodel`.
================================================================

The `mm` model-ADT is too simple to be informative. The complexity of its types exist outside the virtual machine. In order to provide a comprehensive understanding of mm-ADT models, the following sections will build a *property graph* model-ADT (`pg`) in stages starting with `pg_1`, then `pg_2`, so forth before reaching the final complete encoding in `pg`.

==== Constructors

[discrete]
===== Property Graph Model 1

https://en.wikipedia.org/wiki/Graph_theory[Graph theory] acknowledges a variety of graph structures. One such structure is the *property graph*. The more descriptive, yet significantly longer name is the _link:https://en.wikipedia.org/wiki/Directed_graph[directed], attributed, https://en.wikipedia.org/wiki/Multigraph[multi-relational] binary graph_. The breadth of features will ultimately be captured in `pg`. The reduced `pg_1` model only defines a _directed binary graph_. In `pg_1`, a `vertex` can be derived from a `rec` with an `'id'\->int` field. An edge can be derived from a `rec` with an outgoing/start `vertex` (`outV`) and an incoming/end `vertex` (`inV`). The associated diagram graphically captures the `pg_1` structure, where the `.` prefixes on the `inst` morphisms denote the `mmlang` sugar notation for `[get]` -- e.g., `.outV` is sugar for `[get,'outV']`.

[.center]
[cols="11,6",grid=none,frame=none]
|====
a|
[source]
----
include::../../src/main/resources/model/examples/pg_1.mm[]
----
| image:pg_1-model.svg[align=center]

|====

NOTE: A type definition with no instructions serves as both a model constructor and canonical type (a ctype). As a canonical type, the path from source to target does nothing. An `obj` that matches the left-hand side is simply labeled with the name of the `obj` on right-hand side.
\[
\tt{(id->int)}\;\;\tt{\textrm{—[noop]}{\longrightarrow}}\;\;\tt{vertex}.
\]

Three examples of https://en.wikipedia.org/wiki/Constructor_(object-oriented_programming)[constructing] a `vertex` are presented below.

[exec]
----
:[model,pg_1]                                //<1>
('id'->1) => vertex                          ## vertex:('id'->1)  //<2>
('id'->1,'age'->28) => vertex                ## vertex:('id'->1)  //<3>
('ID'->1) => vertex                          ## LanguageException //<4>
----
<1> The type prefix loads the `pg_1` model into the `obj` graph.
<2> A `vertex` from a `rec` with the requisite `'id'` field.
<3> Extraneous (non-ambiguous) in the `vertex` instance is mapped to the terminal \$\mathbf{0}\$.
<4> Coercion to a `vertex` is not possible given as `'ID'` is not `'id'`.

Three `edge` construction examples are presented below.

[exec]
----
:[model,pg_1]
('outV'->vertex:('id'->1),'inV'->vertex:('id'->2)) => edge     ## edge:('outV'->vertex:('id'->1),'inV'->vertex:('id'->2))  //<1>
('outV'->('id'->1),'inV'->('id'->2)) => edge                   ## edge:('outV'->vertex:('id'->1),'inV'->vertex:('id'->2))  //<2>
(vertex:('id'->1);vertex:('id'->2)) => edge                    ## LanguageException //<3>
----
<1> An `edge` is the `rec` product of two `vertices`.
<2> If the components of the product can be coerced into vertices, they are automatically done so.
<3> A `lst` product is not the same as a `rec` product given that `recs` are products of key/value pairs.

==== Type Paths

[discrete]
===== Property Graph Model 2

In the previous `pg_1` model, a `vertex` (`edge`) was constructed using a `rec` with the requisite component structure. After validating the structural type of `rec`, the `rec` is labeled `vertex` (`edge`). There are situations in which the _source_ `obj` has a significantly different absolute structure than the _target_ `obj`. The ways in which an `obj` can be constructed are categorized in the table below where *inline* is for one time use, *define* for repeated use in a program, and *model* for reuse across programs.

[cols="1,9"]
|====

^.^h| inline a|
[exec]
----
:[model,pg_1]
1 => vertex<=int-<('id'->_)                     ## vertex:('id'->1)
----
{blank}
^.^h|define a|
[exec]
----
:[model,pg_1][define,vertex<=int-<('id'->_)]
5 => vertex                                     ## vertex:('id'->5)
(5;6) => (vertex;vertex)                        ## (vertex:('id'->5);vertex:('id'->6))
----
{blank}
^.^h|model a|
[source]
----
include::../../src/main/resources/model/examples/pg_2.mm[]
----
{blank}
|====

image::pg_2-model.svg[width=625,align=center]
<1> The `mm` specification of a canonical `vertex` (an `inst`-less ctype).
<2> A path from an `int` to a `vertex`.
<3> The `mm` specification of a canonical `edge` (an `inst`-less ctype).
<4> A path from a 2-tuple `vertex` `;-lst` to a `edge`.

An edge can be constructed in a number ways. The constructor below maps an `int` pair (\$\mathbb{Z} \times \mathbb{Z}\$) to an `edge` by propagating the pair into the edge product and then constructing vertices for the `outV` and `inV` fields. This structure has sufficient information for rendering the final `edge`. The mm-ADT VM simply names the `rec` pair elements `vertex` and the outer `rec` `edge` thus completing the transformation of an `(int;int)` to `edge` given `pg_1`.

[exec]
----
:[model,pg_2]
(5;6) => (vertex;vertex)=>edge                                              //<1>
(5;6) => edge<=(vertex;vertex)
(5;6) => edge                                                               //<2>
5 => int-<(vertex;vertex) => edge                                           //<3>
5-<(vertex;vertex)=>edge
----
<1> An `int` pair morphed into a `vertex` pair and then into an `edge`.
<2> An `int` pair morphed into an `edge`.
<3> An `int` split into `vertex` clone pairs and then morphed into a https://en.wikipedia.org/wiki/Loop_(graph_theory)[self-loop] edge.

The final example above demonstrates the use of `;-lst` as both a https://en.wikipedia.org/wiki/Coproduct[coproduct] and a https://en.wikipedia.org/wiki/Product_(category_theory)[product] -- i.e., a https://en.wikipedia.org/wiki/Biproduct[biproduct]. The `(vertex;vertex)` pair is created via a split `-<` which serves as the coproduct injections \$\iota_0\$ and \$\iota_1\$. From this vertex coproduct, the `edge` definition projects out each component of via `.0` (`[get,0]`) and `.1` (`[get,1]`). Thus, the coproduct is also a product. For this reason, the https://en.wikipedia.org/wiki/Unicode[Unicode] character for https://en.wikipedia.org/wiki/Pi[pi] (π) (the conventional symbol for product https://en.wikipedia.org/wiki/Projection_(mathematics)[projection]) serves as another `mmlang` sugar for `[get]`.

.model-ADT Application Programming Interfaces
================================================================
The software development pattern espoused by mm-ADT is one in which software libraries (https://en.wikipedia.org/wiki/API[APIs]) are large https://en.wikipedia.org/wiki/Commutative_diagram[commuting diagrams] constructed via domain/range concatenation of \$b<=a\$ types. The diagrams are called *models* and are stored in model-ADT files analogous to `mm.mm`. With a diagram rich in paths, mm-ADT application code will tend towards a _look-and-feel_ similar in form to
\[
a\;{=[} b \Rightarrow c, \; d[x][y][z] \Rightarrow e \Rightarrow f ]\Rightarrow \ldots \dashv z.
\]
where \$d[x][y][z]\$ denotes some intermediate instructions that operate on \$d\$ prior to translating \$d\$ to \$e\$ (i.e., an inline type path) and the connectives that reflect the core operators of the underlying stream ring are:

* `\=>`  : multiplicative monoid for serial composition
* `=[` : additive group for parallel alignment
* `=|`   : non-commutative group for barrier aggregation
================================================================

==== Stream Paths

[discrete]
===== Property Graph Model 3

[source]
----
include::../../src/main/resources/model/examples/pg_3.mm[]
----

==== Higher Order Paths

image::type-to-type-type.png[width=700,align=center]

==== Type Patterns

[cols="^1,4,9"]
|===
| type      | description | mmlang example

| https://en.wikipedia.org/wiki/Anonymous_function[anonymous] | A type with an unspecified domain.
a|
[exec]
----
5 => [plus,2]         ## 7
5 => [plus,[plus,2]]  ## 12
----
{blank}

| https://en.wikipedia.org/wiki/Monomial[monomial] | A https://en.wikipedia.org/wiki/Primitive_data_type[primitive type] that is a single term and coefficient.
a|
[exec]
----
5   => int      ## 5
'5' => int      ## LanguageException
5   => int{10}  ## LanguageException
----
{blank}
| https://en.wikipedia.org/wiki/Polynomial[polynomial] | A https://en.wikipedia.org/wiki/Composite_data_type[composite type] containing a linearly combination of terms and their coefficients.
a|
[exec,exception=LanguageException]
----
(+{2}3,+{3}4,+{4}5)
5-<(+{2}3,+{3}4,+{4}5)
5-<(+{2}3,+{3}4,+{4}5)>-[sum]
----
{blank}
| https://en.wikipedia.org/wiki/Refinement_type[refinement] | A subset of another type.
a|
[exec,exception=LanguageException,none=""]
----
:[define,nat<=int[is>0]]
5 => nat                  ## nat:5
0 => nat                  ## LanguageException
----
{blank}
| https://en.wikipedia.org/wiki/Recursive_data_type[recursive] | A type with components of the same type.
a|
[exec,exception=LanguageException,none=""]
----
:[model,mm][define,list<=[(_){?}\|(_,list)]]
(1)             => list
(1,(1))         => list
(1,(1,(1)))     => list
(1,(1,(1,(1)))) => list
1               => list      ## LanguageException
(1,1)           => list      ## LanguageException
----
{blank}
| https://en.wikipedia.org/wiki/Dependent_type[dependent] | A type with a definition variable to the incoming `obj`.
a|
[exec,none=""]
----
5 => [is>int]    ## {0}
5 => [plus,int]  ## 10
----
{blank}
| https://en.wikipedia.org/wiki/Category_(mathematics)[model] | A set of types and path equations.
a|
[exec,none=""]
----
:[model,social:('type'->  %
  (person -> (person:('name'->str,'age'->nat)), %
   nat    -> (nat<=int[is>0])))]
('name'->'marko','age'->0)                => person  ## LanguageException
('name'->'marko','age'->29)               => person  ## person:('name'->'marko','age'->nat:29)
('name'->'marko','age'->29,'alive'->true) => person  ## person:('name'->'marko','age'->nat:29)

----
{blank}
|===

===== Refinement Types

https://en.wikipedia.org/wiki/Refinement_type[Refinement types] extend a language's base types with https://en.wikipedia.org/wiki/Predicate_(mathematical_logic)[predicates] that further _refine_ (constrain) the base type values. A classic example is the set of natural numbers (\$\mathbb{N}\$) as a refinement of the set of integers (\$\mathbb{Z}\$), where \$\mathbb{N} \subset \mathbb{Z}\$. In https://en.wikipedia.org/wiki/Set-builder_notation#Sets_defined_by_a_predicate[set builder notation], specifying the set of integers and a predicate to limit the set to only those integers greater than 0 is denoted
\[
\mathbb{N} = \{n \in \mathbb{Z} \;|\; n > 0\}.
\]
In mm-ADT, the above is written `int[is>0]` which is the sugar form of `int{?}\<=int[is,[gt,0]]`.

[exec,none=""]
----
:[model,mm][define,nat<=int[is>0]]
10 => nat
-1 => nat                           ## LanguageException
10 => nat[plus,5]
10 => nat[plus,5][plus,-15]         ## LanguageException
----

===== Dependent Types

[exec,none=""]
----
:[model,mm][define,vec:(lst,int)<=lst-<(_,=(_)>-[count]),       %
                          single<=vec:(lst,is<4).0[tail][head], %
                          single<=vec:(lst,is>3).0[head]]
(1;2;3)   => vec                                               //<1>
(1;2;3)   => vec => single                                     //<2>
(1;2;3;4) => vec                                               //<3>
(1;2;3;4) => vec => single                                     //<4>
----
<1> A `;-lst` of 3 terms is morphed into a `vec` using the `vec\<=lst` type.
<2> The `vec` is morphed into a `single` using the first `single\<=vec` type.
<3> A `;-lst` of 4 terms is morphed into a `vec`.
<4> The `vec` is morphed into a `single` using the second `single\<=vec` type.

===== Recursive Types

A recursive type's definition contains a reference to itself. Recursive type definitions require a _base case_ to prevent an infinte recursion. Modern programming languages support generic collections, where a list can be defined to contain a particular type. For example, a `lst` containing only `ints`.

[exec]
----
:[model,mm][define,xlist<=lst[[is,[empty]]|                  %
                              [[is,[head][a,str]];           %
                               [is,[tail][a,xlist]]]]]
( ) => [a,xlist]
('a';'b';'c') => [a,xlist]
('a';'b';'c') => xlist
(1;'a';'c') => xlist              ## LanguageException
('a';'b';'c') => xlist[put,0,3]   ## LanguageException
----

[exec]
----
:[model,mm][define,ylist<=lst[[is,[empty]]|                  %
                              [[is,[head][a,str]];           %
                               [is,[tail][head][a,int]];     %
                               [is,[tail][tail][a,ylist]]]]]
( ) => [a,ylist]
('a';1;'b';2) => [a,ylist]
('a';1;'b';2) => ylist
(1;'a';'c') => ylist              ## LanguageException
('a';1;'b';2) => ylist[put,0,3]   ## LanguageException
----